RENDER_FRAME(frame)
BEGIN
  LOCAL frameData := FRAMES[frame];
  LOCAL frameDataLength := (SIZE(frameData) - 1) / 2;
  LOCAL direction := frameData[1];
  LOCAL major := 0;
  LOCAL minor := 0;
  LOCAL i;

  LOCAL sideLength;

  IF direction == HORIZONTAL THEN
    sideLength := 320;
  ELSE
    sideLength := 240;
  END;

  FOR i FROM 1 TO frameDataLength DO
    LOCAL delta := frameData[i * 2];
    LOCAL chunkSize := frameData[i * 2 + 1];

    IF delta == NO_CHANGE THEN
      major := major + chunkSize;

      IF major >= sideLength THEN
        minor := minor + FLOOR(major / sideLength);
        major := major MOD sideLength;
      END;

      CONTINUE;
    END;

    WHILE chunkSize > 0 DO
      LOCAL usableChunkSize := MIN(sideLength - major, chunkSize);

      IF direction == HORIZONTAL THEN
        IF delta == BLACK THEN
          RECT_P(major, minor, major + usableChunkSize - 1, minor, #000000h);
        ELSE
          RECT_P(major, minor, major + usableChunkSize - 1, minor, #FFFFFFh);
        END;
      ELSE
        IF delta == BLACK THEN
          RECT_P(minor, major, minor, major + usableChunkSize - 1, #000000h);
        ELSE
          RECT_P(minor, major, minor, major + usableChunkSize - 1, #FFFFFFh);
        END;
      END;

      chunkSize := chunkSize - usableChunkSize;
      major := major + usableChunkSize;

      IF major == sideLength THEN
        minor := minor + 1;
        major := 0;
      END;
    END;
  END;
END;
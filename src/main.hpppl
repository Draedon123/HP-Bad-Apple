CONST KEY_ESC = 4;

CONST FPS := 30;
CONST FRAME_TIME = 1 / FPS;

EXPORT MAIN()
BEGIN
  LOCAL lastTick_ms := TICKS();
  LOCAL frame = 1;

  WAIT(FRAME_TIME);

  WHILE true DO
    IF ISKEYDOWN(KEY_ESC) THEN
      KILL;
    END;

    IF frame > SIZE(FRAMES) THEN
      FREEZE;
    END;

    LOCAL tickTime_ms := TICKS();
    LOCAL deltaTime_s := 0.001 * (tickTime_ms - lastTick_ms);
    lastTick_ms := tickTime_ms;

    if deltaTime_s < FRAME_TIME THEN
      WAIT(FRAME_TIME - deltaTime_s);
    END;

    LOCAL frameData := FRAMES[frame];
    LOCAL frameDataLength := SIZE(frameData);
    LOCAL draw := frameData(1);
    LOCAL i := 2;
    LOCAL x := 0;
    LOCAL y := 0;

    RECT(RGB(0, 0, 0));

    WHILE i < frameDataLength DO
      LOCAL chunk := frameData(i);

      WHILE chunk > 0 DO
        LOCAL usableChunkSize := MIN(319 - x, chunk);

        IF draw == 1 THEN
          LINE_P(x, y, x + usableChunkSize, y, RGB(255, 255, 255));
        END;

        chunk := chunk - usableChunkSize;
        x := x + usableChunkSize;

        IF x == 319 THEN
          y := y + 1;
          x := 0;
        END;
      END;

      draw := 1 - draw;
      i := i + 1;
    END;

    frame := frame + 1;
  END;
END;
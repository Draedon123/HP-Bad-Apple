CONST KEY_ESC = 4;

CONST FPS := 30;
CONST FRAME_TIME = 1 / FPS;

// index 0 is the last element, black, and index 1 is the first, white
CONST COLOURS := {RGB(255, 255, 255), RGB(0, 0, 0)};

EXPORT MAIN()
BEGIN
  LOCAL running := true;
  LOCAL lastTick_ms := TICKS();
  LOCAL frame = 1;

  WAIT(FRAME_TIME);

  WHILE running DO
    IF ISKEYDOWN(KEY_ESC) THEN
      running := false;
      KILL;
    END;

    IF frame > SIZE(FRAMES) THEN
      running := false;
      FREEZE;
    END;

    LOCAL tickTime_ms := TICKS();
    LOCAL deltaTime_s := 0.001 * (tickTime_ms - lastTick_ms);
    lastTick_ms := tickTime_ms;

    if deltaTime_s < FRAME_TIME THEN
      WAIT(FRAME_TIME - deltaTime_s);
    END;

    LOCAL frameData := FRAMES[frame];
    LOCAL frameDataLength := SIZE(frameData);
    LOCAL colourIndex := frameData(1);
    LOCAL i := 2;
    LOCAL x := 0;
    LOCAL y := 0;

    WHILE i < frameDataLength DO
      LOCAL chunk := frameData(i);

      WHILE chunk > 0 DO
        LOCAL usableChunkSize := MIN(319 - x, chunk);
        LINE_P(x, y, x + usableChunkSize, y, COLOURS(colourIndex));

        chunk := chunk - usableChunkSize;
        x := x + usableChunkSize;

        IF x == 319 THEN
          y := y + 1;
          x := 0;
        END;
      END;

      colourIndex := 1 - colourIndex;
      i := i + 1;
    END;

    frame := frame + 1;
  END;
END;